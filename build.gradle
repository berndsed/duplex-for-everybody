plugins {
    id 'groovy'
}

group 'de.kieseltaucher.duplex.scan.for.everybody'
version '1.0-SNAPSHOT'

task asGroovyScript(type: Zip) {
    description 'Creates a distribution containing a groovy script'
    group 'distribution'

    from layout.projectDirectory.file('src/main/groovy')
    from layout.projectDirectory.file('distributions/groovy-script/simplex2duplex.groovy')
    archiveAppendix = 'groovy-script'
}

task asAwsLambda(type: Zip) {
    description 'Creates an aws lambda deployment package'
    group 'distribution'

    from compileGroovy
    into('lib') {
        from configurations.runtimeClasspath
    }
    archiveAppendix = 'aws-lambda'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy:2.5.8'
    implementation 'org.codehaus.groovy:groovy-json:2.5.8'
    implementation 'org.apache.pdfbox:pdfbox:2.0.24'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}
